buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-2'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'com.moowork.node'

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    maven { url "http://dl.bintray.com/danfma/maven" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    compile "br.danfma:kodando-runtime:2017.8.25"
    testCompile "org.jetbrains.kotlin:kotlin-test-js"
    testCompile "br.danfma:kodando-jest:2017.8.25-js-19.0"
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
}

task installJest(type: NpmTask) {
    args = ['install', 'jest']
}

task runJest(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installJest]) {
    script = file('node_modules/.bin/jest')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runJest
